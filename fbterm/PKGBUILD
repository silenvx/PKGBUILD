# Maintainer: silenvx <czvziwzx at gmail.com>

pkgname=fbterm
pkgver=1.7.0
pkgrel=6
pkgdesc="A fast terminal emulator for linux with frame buffer device or VESA video card"
arch=('armv6h' 'i686' 'x86_64')
url="http://code.google.com/p/fbterm/"
license=('GPL2')
makedepends=('gpm')
depends=('gcc-libs' 'fontconfig')
optdepends=('libx86: for VESA video card support'
            'gpm: for mouse support')
install=fbterm.install
source=("http://fbterm.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
        "fbterm-1.7-term.patch"
        "config_colors.patch")

md5sums=('c36bae75a450df0519b4527cccaf7572'
         '2bbf2e07f19c7b1a39e71d26ea762e9d'
         '105dd584155506ebaf8a33ca4c9ee0a9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.?}"
# http://code.google.com/p/fbterm/issues/attachmentText?id=48&aid=480002000&name=fbterm-1.7-term.patch
  patch -p1 -i "${srcdir}/fbterm-1.7-term.patch"

# http://d.hatena.ne.jp/silenvx/20140319/1395233169
  patch -p1 -i "${srcdir}/config_colors.patch"

  ./configure --prefix=/usr --enable-gpm
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.?}"

  install -Dm644 terminfo/fbterm "${pkgdir}/usr/share/terminfo/f/fbterm"

  make DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install
}
