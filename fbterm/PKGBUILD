# Maintainer: silenvx <czvziwzx at gmail.com>

pkgname=fbterm
pkgver=1.7.0
pkgrel=5
pkgdesc="A fast terminal emulator for linux with frame buffer device or VESA video card"
arch=('i686' 'x86_64')
url="http://code.google.com/p/fbterm/"
license=('GPL2')
depends=('gcc-libs' 'fontconfig')
optdepends=('libx86: for VESA video card support')
install=fbterm.install
source=("http://fbterm.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
        "fbterm-1.7-term.patch")

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.?}"
  patch -p1 -i "${srcdir}/fbterm-1.7-term.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.?}"

  install -Dm644 terminfo/fbterm "${pkgdir}/usr/share/terminfo/f/fbterm"

  make DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install
}

md5sums=('c36bae75a450df0519b4527cccaf7572'
         '2bbf2e07f19c7b1a39e71d26ea762e9d')
