# Maintainer: silenvx <czvziwzx at gmail.com>

pkgname=mutt
pkgver=1.5.23
pkgrel=1
pkgdesc='Small but very powerful text-based mail client'
url='http://www.mutt.org/'
license=('GPL')
backup=('etc/Muttrc')
arch=('armv6h' 'i686' 'x86_64')
optdepends=('smtp-forwarder: to send mail')
depends=('gpgme' 'ncurses' 'openssl' 'libsasl' 'gdbm' 'libidn' 'mime-types' 'krb5')
source=("ftp://ftp.mutt.org/mutt/${pkgname}-${pkgver}.tar.gz"
        'source_complete.patch')
sha1sums=('8ac821d8b1e25504a31bf5fda9c08d93a4acc862'
          '7dea4dcfba3fa739b63c134ab81234b61ac828b3')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
# :sourceで補完を使えるようにする
# http://d.hatena.ne.jp/silenvx/20130403/1364932873
    patch -p1 -i "${srcdir}/source_complete.patch"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-gpgme \
		--enable-pop \
		--enable-imap \
		--enable-smtp \
		--enable-hcache \
		--with-curses=/usr \
		--with-regex \
		--with-gss=/usr \
		--with-ssl=/usr \
		--with-sasl \
		--with-idn \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/etc/mime.types{,.dist}
	rm "${pkgdir}"/usr/bin/{flea,muttbug}
	rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
	install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
