# Maintainer: silenvx <czvziwzx at gmail.com>

pkgname=tmux
pkgver=1.8
pkgrel=1
pkgdesc='A terminal multiplexer'
url='http://tmux.sourceforge.net/'
arch=('i686' 'x86_64')
license=('BSD')
depends=('ncurses' 'libevent')
source=(http://jaist.dl.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz
        LICENSE
        borders.patch
        utf8_width.patch
        copy_cursor_width.patch
        tmux-fbterm.patch)
md5sums=('b9477de2fe660244cbc6e6d7e668ea0e'
         '71601bc37fa44e4395580b321963018e'
         'ebf01f342a3007f9515e50ddf4c883cf'
         'ed195ffa2573ed8a503f20f823a47006'
         '046828137746521e8bf88ca493b90ca2'
         '54a6339ecc1ff954be205dc13c3bb33b')

build() {
	cd "$srcdir/$pkgname-$pkgver"
    patch -p1 -i $srcdir/borders.patch
    patch -p1 -i $srcdir/utf8_width.patch
    patch -p1 -i $srcdir/copy_cursor_width.patch
    patch -p0 -i $srcdir/tmux-fbterm.patch
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make install DESTDIR=$pkgdir
	install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/tmux/LICENSE"

	install -dm755 "$pkgdir/usr/share/tmux/"
	install -m644 examples/* "$pkgdir/usr/share/tmux/"
	install -Dm644 examples/tmux.vim "$pkgdir/usr/share/vim/vimfiles/syntax/tmux.vim"

	install -d $pkgdir/usr/share/bash-completion/completions/
	mv $pkgdir/usr/share/tmux/bash_completion_tmux.sh $pkgdir/usr/share/bash-completion/completions/tmux
}
