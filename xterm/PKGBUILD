# Maintainer: silenvx <czvziwzx at gmail.com>

pkgname=xterm
pkgver=324
pkgrel=1
pkgdesc="X Terminal Emulator"
arch=('armv6h' 'i686' 'x86_64')
url="http://invisible-island.net/xterm/"
license=('custom')
depends=('libxft' 'libxaw' 'ncurses' 'xorg-luit' 'xbitmaps' 'libutempter')
source=(ftp://invisible-island.net/${pkgname}/${pkgname}-${pkgver}.tgz{,.asc}
        LICENSE
        locale.patch)
md5sums=('7c325c29d67637cf7ff8e904a23c222e'
         'SKIP'
         '10ecc3f8ee91e3189863a172f68282d2'
         '914ad0f9ae857ff8c67554d48091e612')
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB') # "Thomas Dickey <dickey@invisible-island.net>"
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
# http://d.hatena.ne.jp/silenvx/20121015/1350233551
  patch -p1 -i "${srcdir}/locale.patch"
  ./configure --prefix=/usr \
      --libdir=/etc \
      --mandir=/usr/share/man \
      --with-app-defaults=/usr/share/X11/app-defaults/ \
      --with-x \
      --disable-full-tgetent \
      --disable-imake \
      --enable-ansi-color \
      --enable-88-color \
      --enable-256-color \
      --enable-broken-osc \
      --enable-broken-st \
      --enable-load-vt-fonts \
      --enable-i18n \
      --enable-wide-chars \
      --enable-doublechars \
      --enable-warnings \
      --enable-tcap-query \
      --enable-logging \
      --enable-dabbrev \
      --enable-freetype \
      --enable-luit \
      --enable-mini-luit \
      --enable-narrowproto \
      --enable-exec-xterm \
      --with-tty-group=tty \
      --with-utempter
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  chmod 0755 "${pkgdir}/usr/bin/xterm"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" \
      "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m755 -d ${pkgdir}/usr/share/applications
  install -m644 ${srcdir}/${pkgname}-${pkgver}/{xterm,uxterm}.desktop ${pkgdir}/usr/share/applications/
}
